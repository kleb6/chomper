#!/usr/bin/env raku 

#use Grammar::Tracer;

use Chomper::Core::Util;
use Chomper::Core::Comments;
use Chomper::Core::DoxyComment;
use Chomper::Core::ReformatBlockComment;
use Chomper::Core::BlockComment;
use Chomper::Core::LineCommentToBlockComment;
use Chomper::Core::Typemap;
use Chomper::Core::Locations;
use Chomper::Translate::Translator;
use Chomper::Core::SnakeCase;

our sub get-hook($hookname) {

    #key matches grammar rules
    #will return one of these values
    my %dispatch = %(

        default-ctor                          => { use Chomper::Core::Constructor;        &translate-default-ctor },
        #python-function-header                => { use Chomper::Py3::PythonFunctionHeader; &translate-python-function-header },
        simple-ifdef                          => { use Chomper::Core::SimpleIfdef;       &translate-simple-ifdef },
        hashing-function                      => { use Chomper::Core::Hasher;             &translate-hasher },
        op-add-eq-function-header             => { use Chomper::Core::OpAdd;             &translate-op-add-eq },
        op-bitor-assign-function-header       => { use Chomper::Core::OpBitor;           &translate-op-bitor-assign },
        op-bitand-assign-function-header      => { use Chomper::Core::OpBitand;          &translate-op-bitand-assign },
        op-add-function-header                => { use Chomper::Core::OpAdd;             &translate-op-add },
        function-declaration                  => { use Chomper::Core::Function;           &translate-function },
        constructor-definition-header         => { use Chomper::Core::Constructor;        &translate-ctor },
        op-div-function-header                => { use Chomper::Core::OpDiv;             &translate-op-div },
        op-div-eq-function-header             => { use Chomper::Core::OpDivEq;          &translate-op-div-eq },
        op-eq-function-header                 => { use Chomper::Core::OpEq;              &translate-op-eq },
        op-lt-function-header                 => { use Chomper::Core::OpEq;              &translate-op-lt },
        op-index-function-header              => { use Chomper::Core::OpIndex;           &translate-op-index },
        op-xor-function-header                => { use Chomper::Core::OpXor;             &translate-op-xor },
        op-bitand-function-header             => { use Chomper::Core::OpBitand;          &translate-op-bitand },
        op-bitor-function-header              => { use Chomper::Core::OpBitor;           &translate-op-bitor },
        op-mul-function-header                => { use Chomper::Core::OpMul;             &translate-op-mul },
        op-mul-eq-function-header             => { use Chomper::Core::OpMulEq;          &translate-op-mul-eq },
        op-negate-function-header             => { use Chomper::Core::OpNegate;          &translate-op-negate },
        op-sub-function-header                => { use Chomper::Core::OpSub;             &translate-op-sub },
        op-sub-eq-function-header             => { use Chomper::Core::OpSub;             &translate-op-sub-eq },
        freestanding-template-function-header => { use Chomper::Core::Template;           &translate-freestanding-template-function },
        op-ostream-function-header            => { use Chomper::Core::Ostream;            &translate-op-ostream },
        op-convert-function-header            => { use Chomper::Core::OpConvert;         &translate-op-convert },
        static-constants                      => { use Chomper::Core::StaticConst;       &translate-static-const },
        function-local-declarations           => { use Chomper::Core::FunctionLocals;    &translate-function-locals },
        struct-member-declarations            => { use Chomper::Core::StructMembers;     &translate-struct-member-declarations },
        enum-member-declarations              => { use Chomper::Core::EnumMembers;       &translate-enum-member-declarations },
        full-struct                           => { use Chomper::Core::FullStruct;        &translate-full-struct },
        full-enum                             => { use Chomper::Core::FullEnum;          &translate-full-enum },
        abstract-function-declarations        => { use Chomper::Core::AbstractFunctions; &translate-abstract-function-declarations },
        destructor                            => { use Chomper::Core::Destructor;         &translate-destructor },
        ctor-header                           => { use Chomper::Core::CtorHeader;        &translate-ctor-header },
        using-declarations                    => { use Chomper::Core::UsingDeclarations; &translate-using-declarations },
        typedef-fn-ptrs                       => { use Chomper::Core::TypedefFnPtr;     &translate-typedef-fn-ptrs },
        pound-define                          => { use Chomper::Core::PoundDefine;       &translate-pound-define },
        constexpr-global-block                => { use Chomper::Core::ConstexprGlobals;  &translate-constexpr-global-block },
        operator-into-bool                    => { use Chomper::Core::OperatorIntoBool; &translate-operator-into-bool },
        operator-indirect                     => { use Chomper::Core::OperatorIndirect;  &translate-operator-indirect },
        operator-not                          => { use Chomper::Core::OperatorNot;       &translate-operator-not },
        op-shl-function-header                => { use Chomper::Core::OpShift;           &translate-op-shl },
        op-shl-assign-function-header         => { use Chomper::Core::OpShift;           &translate-op-shl-assign },
        op-shr-function-header                => { use Chomper::Core::OpShift;           &translate-op-shr },
        op-shr-assign-function-header         => { use Chomper::Core::OpShift;           &translate-op-shr-assign },
    );

    %dispatch{$hookname}()
}

sub success-cleanup() {
    sort-uniq-snake-case-file()
}

sub maybe-extract-comment(Match $m) {
    if $m<block-comment> {
        return parse-doxy-comment(~$m<block-comment>).chomp.trim;
    }

    if $m<line-comment> {

        my $line-comment  = format-rust-comments(get-rcomments-list($m));

        return parse-doxy-comment(
            line-comment-to-block-comment($line-comment)
        ).chomp.trim;
    }

    return Nil;
}

sub maybe-extract-func-tags(Match $m) {

    if $m<func-tags> {

        do for $m<func-tags><func-tag>.List {
            "#[{$_.Str.chomp.trim}]"
        }.join("\n")

    } else {
        Nil
    }
}

sub MAIN(
    :$chop = False, 
    :$class = Nil, 
    :$de-semicolon = False) { 

    #this makes it so we don't have to recompile 
    #every time we want to change the typemap
    my %*typemap = %typemap;

    #add whitelisted types to typemap
    #a whitelisted type is the same in Rust as it is in c++
    for $whitelist-file.IO.slurp.lines {
        %*typemap{$_} = $_;
    }

    for $text-typemap-file.IO.slurp.lines {
        my @tokens = $_.split(" ");
        my $head = @tokens[0];
        my $tail = @tokens[1..*].join(" ");
        %*typemap{$head} = $tail;
    }

    my $in = $*IN.slurp.chomp;

    if $de-semicolon {
        $in = $in.subst(rule { ';' $ }, '{ }');
    }

    my ($head, $body) = get_parts($in, $chop);

    my $match     
    = Translator.parse($head);

    my $hookname = ~$match<hook>.keys[0];
    my &hook      = &get-hook($hookname);

    my $comment  = maybe-extract-comment($match);
    my $submatch = $match<hook>{$hookname};
    my $tags     = maybe-extract-func-tags($submatch);

    if $submatch {

        given ($comment.WHAT, $tags.WHAT) {
            when (Str, Str) {
                say qq:to/END/;
                $comment
                $tags
                {&hook($submatch, $body, $class)}
                END
            }
            when (Str, Any) {
                say qq:to/END/;
                $comment
                {&hook($submatch, $body, $class)}
                END

            }
            when (Any, Str) {
                say qq:to/END/;
                $tags
                {&hook($submatch, $body, $class)}
                END

            }
            when (Any, Any) {
                say qq:to/END/;
                {&hook($submatch, $body, $class)}
                END
            }
        }

        success-cleanup();

    } else {

        say "could not parse submatch";
    }
}
