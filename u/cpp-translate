#!/usr/bin/env raku
use Chomper::Cpp::CppActions;
use Chomper::Cpp::Gcpp;
use Chomper::TranslateIr;
use Chomper::TranslateIo;
use Chomper::Typemap;
use Chomper::Locations;
use Data::Dump::Tree;

grammar G does CppParser {}

use Grammar::Tracer;
grammar GD does CppParser {}

sub MAIN(:$stdin = Nil, Str :$rule="statement-seq") {

    #this makes it so we don't have to recompile
    #every time we want to change the typemap
    my %*typemap = %typemap;

    #add whitelisted types to typemap
    #a whitelisted type is the same in Rust as it is in c++
    for $whitelist-file.IO.slurp.lines {
        %*typemap{$_} = $_;
    }

    for $text-typemap-file.IO.slurp.lines {
        my @tokens = $_.split(" ");
        my $head = @tokens[0];
        my $tail = @tokens[1..*].join(" ");
        %*typemap{$head} = $tail;
    }

    my $in = $stdin 
        ?? $stdin.IO.slurp.chomp 
        !! $*IN.slurp.chomp;

    $in = $in.subst(/^\s+/,"");

    #my $rule = "parameter-declaration-body";
    #my $rule = "statement-seq"; #usually want this one

    my $actions = CppActions.new;

    my $parsed = 
    G.parse($in, :$rule, :$actions) 
    // 
    GD.parse($in, :$rule, :$actions);

    if $parsed {

        say $parsed;

        my $made = $parsed.made;

        ddt $made;
        say $made>>.gist(treemark => True).join("\n");

        say "---------------------[gists]";

        for $made.List>>.gist.join("") {
            say $_.gist;
        }

        my $stripped      = $made.List>>.gist.join("").subst(:g, /<.ws>/,"");
        my $orig-stripped = $in.subst(:g, /<.ws>/,"");

        if not $stripped eq $orig-stripped {
            say "-----------------------------";
            say "round trip bug!!!";
            say "";
            say "orig:  $orig-stripped";
            say "rtrip: $stripped";
            die;
        }

        say "-------------[cpp round trip success]-------------";

        translate-ir(
            $made, 
            TranslationSource::<LangCpp>, 
            TranslationTarget::<LangRust>
        );
    }
}
